import std/[os, strutils, math]

type Body = object
  x, y, z, vx, vy, vz, mass: float64

const
  SolarMass = 4.0 * PI * PI
  DaysPerYear = 365.24

proc initBody(x, y, z, vx, vy, vz, mass: float64): Body =
  Body(x: x, y: y, z: z, vx: vx * DaysPerYear, vy: vy * DaysPerYear, vz: vz * DaysPerYear, mass: mass * SolarMass)

var bodies = [
  Body(x: 0, y: 0, z: 0, vx: 0, vy: 0, vz: 0, mass: SolarMass),
  initBody(4.84143144246472090e+00, -1.16032004402742839e+00, -1.03622044471123109e-01, 1.66007664274403694e-03, 7.69901118419740425e-03, -6.90460016972063023e-05, 9.54791938424326609e-04),
  initBody(8.34336671824457987e+00, 4.12479856412430479e+00, -4.03523417114321381e-01, -2.76742510726862411e-03, 4.99852801234917238e-03, 2.30417297573763929e-05, 2.85885980666130812e-04),
  initBody(1.28943695621391310e+01, -1.51111514016986312e+01, -2.23307578892655734e-01, 2.96460137564761618e-03, 2.37847173959480950e-03, -2.96589568540237556e-05, 4.36624404335156298e-05),
  initBody(1.53796971148509165e+01, -2.59193146099879641e+01, 1.79258772950371181e-01, 2.68067772490389322e-03, 1.62824170038242295e-03, -9.51592254519715870e-05, 5.15138902046611451e-05)
]

proc advance(bodies: var openArray[Body], dt: float64) =
  for i in 0..<bodies.len:
    for j in i+1..<bodies.len:
      let dx = bodies[i].x - bodies[j].x
      let dy = bodies[i].y - bodies[j].y
      let dz = bodies[i].z - bodies[j].z
      let d2 = dx*dx + dy*dy + dz*dz
      let mag = dt / (d2 * sqrt(d2))
      bodies[i].vx -= dx * bodies[j].mass * mag
      bodies[i].vy -= dy * bodies[j].mass * mag
      bodies[i].vz -= dz * bodies[j].mass * mag
      bodies[j].vx += dx * bodies[i].mass * mag
      bodies[j].vy += dy * bodies[i].mass * mag
      bodies[j].vz += dz * bodies[i].mass * mag
  for i in 0..<bodies.len:
    bodies[i].x += dt * bodies[i].vx
    bodies[i].y += dt * bodies[i].vy
    bodies[i].z += dt * bodies[i].vz

proc energy(bodies: openArray[Body]): float64 =
  var e = 0.0
  for i in 0..<bodies.len:
    e += 0.5 * bodies[i].mass * (bodies[i].vx*bodies[i].vx + bodies[i].vy*bodies[i].vy + bodies[i].vz*bodies[i].vz)
    for j in i+1..<bodies.len:
      let dx = bodies[i].x - bodies[j].x
      let dy = bodies[i].y - bodies[j].y
      let dz = bodies[i].z - bodies[j].z
      e -= (bodies[i].mass * bodies[j].mass) / sqrt(dx*dx + dy*dy + dz*dz)
  return e

proc offsetMomentum(bodies: var openArray[Body]) =
  var px, py, pz = 0.0
  for b in bodies:
    px += b.vx * b.mass
    py += b.vy * b.mass
    pz += b.vz * b.mass
  bodies[0].vx = -px / SolarMass
  bodies[0].vy = -py / SolarMass
  bodies[0].vz = -pz / SolarMass

let n = if paramCount() > 0: parseInt(paramStr(1)) else: 10_000_000
offsetMomentum(bodies)
echo formatFloat(energy(bodies), ffDecimal, 9)
for _ in 1..n: advance(bodies, 0.01)
echo formatFloat(energy(bodies), ffDecimal, 9)
